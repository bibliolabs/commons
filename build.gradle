buildscript {

    repositories {
        jcenter()
    }
}

allprojects {

    group = 'com.chillenious.commons'
    version = currentVersion

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'


    publishing {
        repositories {
            maven {
                url "s3://bibliolabs-repository/ext-release-local"
                credentials(AwsCredentials) {
                    accessKey repository_user
                    secretKey repository_password
                }
            }
        }
    }
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadSources = true
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    configurations {
        archives
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    buildscript {

        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven { url "http://clojars.org/repo" }
        }

        dependencies {
            classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
            classpath 'net.saliman:gradle-properties-plugin:1.4.2'
        }
    }
}

// http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
        provider.node.component.find { it.@name == 'ProjectRootManager' }.@"assert-jdk-15" = 'true'
        provider.node.component.find { it.@name == 'ProjectRootManager' }.@languageLevel = 'JDK_1_8'
    }
}
