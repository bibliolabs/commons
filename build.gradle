buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
    }
}

allprojects {

    group = 'com.chillenious.commons'
    version = currentVersion

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    apply plugin: 'com.jfrog.bintray'

    ext.publish = true

    bintray {
        user = bintray_user
        key = bintray_api_key
        configurations = ['archives']
        dryRun = false // Whether to run this as dry-run, without deploying
        publish = true //If version should be auto published after an upload
        pkg {
            repo = 'maven'
            name = 'chillenious-commons'
            desc = 'Little bag of tools for Guice based projects'
            websiteUrl = 'https://github.com/chillenious/commons'
            issueTrackerUrl = 'https://github.com/chillenious/commons/issues'
            vcsUrl = 'https://github.com/chillenious/commons.git'
            licenses = ['MIT']
            labels = ['Guice', 'Java', 'jOOQ', 'bibliolabs', 'onespot']
            publicDownloadNumbers = true
        }
    }
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadSources = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    configurations {
        archives
    }

    artifacts {
        archives sourceJar
    }

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    buildscript {

        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven { url "http://clojars.org/repo" }
        }

        dependencies {
            classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
            classpath 'net.saliman:gradle-properties-plugin:1.4.2'
        }
    }
}

// http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
        provider.node.component.find { it.@name == 'ProjectRootManager' }.@"assert-jdk-15" = 'true'
        provider.node.component.find { it.@name == 'ProjectRootManager' }.@languageLevel = 'JDK_1_8'
    }
}
