buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
    }
}

allprojects {

    group = 'com.chillenious.commons'
    version = currentVersion

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    ext.publish = true

    publishing {
        repositories {
            maven {
                url "${artifactory_contextUrl}/libs-release-local"
                credentials {
                    username = artifactory_user
                    password = artifactory_password
                }
            }
        }
    }
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadSources = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    configurations {
        archives
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

        artifacts {
        archives sourceJar
    }

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    buildscript {

        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven { url "http://clojars.org/repo" }
        }

        dependencies {
            classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
            classpath 'net.saliman:gradle-properties-plugin:1.4.2'
        }
    }
}

// http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
        provider.node.component.find { it.@name == 'ProjectRootManager' }.@"assert-jdk-15" = 'true'
        provider.node.component.find { it.@name == 'ProjectRootManager' }.@languageLevel = 'JDK_1_8'
    }
}
